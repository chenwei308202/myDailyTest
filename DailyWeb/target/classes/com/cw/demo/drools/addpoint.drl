package com.cw.demo.drools;
import com.cw.demo.drools.PointDomain
import java.awt.Point

dialect  "mvel"
//过生日，则加10分，并且将当月交易笔数翻倍后再计算积分
rule "birthdayPoint"
    salience 100
    lock-on-active true
    when
        $pointDomain:PointDomain(birthDay==true)

    then
        $pointDomain.setPoint($pointDomain.getPoint()+10);
        $pointDomain.setBuyNums($pointDomain.getBuyNums()*2);
        $pointDomain.setBuyMoney($pointDomain.getBuyMoney()*2);
        $pointDomain.setBillThisMonth($pointDomain.getBillThisMonth()*2);
        $pointDomain.recordPointLog($pointDomain.getUserName(),"birthdayPoint");
end
//2011-01-08 到 2011-08-08 每月信用卡还款3次以上，每满3笔赠送30分
rule "billThisMonthPoint"
    salience 99
    lock-on-active true
    date-effective "2011-01-08 23:59:59"
    date-expires "2011-08-08 23:59:59"
    when
        $pointDomain:PointDomain(billThisMonth>=3)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+$pointDomain.getBillThisMonth()/3*30);
        $pointDomain.recordPointLog($pointDomain.getUserName(),"billThisMonthPoint");
end
//当月购物总金额100以上，每100元赠送10分
rule "buyMoneyPoint"
    salience 98
    lock-on-active true
    when
        $pointDomain:PointDomain(buyMoney>=100)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+(int)$pointDomain.getBuyMoney()/100*10);

        $pointDomain.recordPointLog($pointDomain.getUserName(),"buyMoneyPoine");

end
//当月购物次数5次以上，每五次赠送50分
rule "buyNumsPoint"
    salience 97
    lock-on-active true
    when
        $pointDomain:PointDomain(buyNums>=5)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+(int)$pointDomain.getBuyNums()/5*50);

        $pointDomain.recordPointLog($pointDomain.getUserName(),"buyNumPoine");

end

//满足全部要求，额外奖励100分
rule "fitPoint"
    salience 96
    lock-on-active true
    when
        $pointDomain:PointDomain(buyNums>=5 && billThisMonth>=3 && buyMoney>=100)
    then
        $pointDomain.setPoint($pointDomain.getPoint()+100);

        $pointDomain.recordPointLog($pointDomain.getUserName(),"allFitPoint");

end